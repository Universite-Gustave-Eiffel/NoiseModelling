plugins {
    id 'java'
    id 'groovy'
    id 'maven-publish'
    id 'application'
    id 'distribution'
}


sourceCompatibility = 11


distributions {
    main {
        distributionBaseName = 'scriptrunner'
        contents {
            from '../LICENSE'
            from '../README.md'
            from 'get_started_tutorial.sh'
            from 'src/main/groovy/org/noise_planet/'
            from 'src/main/groovy/get_started_tutorial.groovy'
            from('src/test/') { include 'resources/**' }
        }
    }
}

startScripts {
    doLast {
        windowsScript.text = windowsScript.text.replaceAll('set CLASSPATH=.*', 'set CLASSPATH=.;%APP_HOME%/lib/*')
    }
}


compileJava {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

publishing {
    publications {
        myLibrary(MavenPublication) {
            from components.java
        }
    }

    repositories {
        mavenLocal()
    }
}
repositories {
    mavenLocal() // To test noise modelling updated locally we have to put this first
    mavenCentral()
    maven {
        url "libs/"
    }
    maven {
        url "https://repo.osgeo.org/repository/release/"
    }
    maven {
        url "https://download.osgeo.org/webdav/geotools/"
    }
    maven {
        url "https://maven.geo-solutions.it/"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/releases/"
    }
    maven {
        url "https://repo.matsim.org/repository/matsim/"
    }
}

test {
    afterTest { desc, result ->
        println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}
dependencies {
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.4'
    implementation('org.geotools:gt-jdbc:21.1') {
        exclude group: 'javax.media'
    }
    implementation('org.geoserver.script:gs-script-groovy:2.15.1') {
        exclude group: 'org.geoscript'
        exclude group: 'no.ecc.vectortile'
        exclude group: 'javax.media'
        exclude group: 'commons-beanutils'
        exclude group: 'org.locationtech.jts'
        exclude group: 'org.geoserver', module: 'gs-wms'
        exclude group: 'org.geoserver', module: 'gs-wfs'
        exclude group: 'org.geoserver', module: 'gs-ows'
        exclude group: 'org.geoserver', module: 'gs-rest'
        exclude group: 'org.geoserver', module: 'gs-restconfig'
        exclude group: 'org.geoserver', module: 'gs-main'
        exclude group: 'org.geoserver', module: 'gs-wcs2_0'
        exclude group: 'org.geoserver', module: 'gs-wcs1_1'
        exclude group: 'org.geoserver', module: 'gs-wcs1_0'
        exclude group: 'org.geotools.xsd', module: 'gt-xsd-sld'
        exclude group: 'org.geotools.xsd', module: 'gt-xsd-wps'
        exclude group: 'org.geotools', module: 'gt-process-raster'
        exclude group: 'org.geotools', module: 'gt-process-feature'
    }
    implementation 'org.postgresql:postgresql:42.7.4'
    implementation 'org.codehaus.groovy:groovy-all:2.5.5'
    implementation group: 'org.ejml', name: 'all', version: '0.29'
    implementation group: 'org.eclipse.emf', name: 'org.eclipse.emf.ecore', version: '2.10.1'
    implementation group: 'org.orbisgis', name: 'h2gis', version: '2.2.3'
    implementation('org.locationtech.jts:jts-core:1.20.0')
    implementation('org.locationtech.jts:jts-io:1.20.0')
    implementation group: 'org.orbisgis', name: 'noisemodelling-emission', version: '5.0.0-SNAPSHOT'
    implementation group: 'org.orbisgis', name: 'noisemodelling-propagation', version: '5.0.0-SNAPSHOT'
    implementation group: 'org.orbisgis', name: 'noisemodelling-pathfinder', version: '5.0.0-SNAPSHOT'
    implementation group: 'org.orbisgis', name: 'noisemodelling-jdbc', version: '5.0.0-SNAPSHOT'
    implementation group: 'org.osgi', name: 'org.osgi.service.jdbc', version: '1.0.0'
    implementation group: 'org.openstreetmap.osmosis', name: 'osmosis-core', version: '0.48.3'
    implementation group: 'org.openstreetmap.osmosis', name: 'osmosis-pbf', version: '0.48.3'
    implementation group: 'org.openstreetmap.osmosis', name: 'osmosis-xml', version: '0.48.3'
    implementation(group: 'org.matsim', name: 'matsim', version: '11.0') {
        exclude group: 'org.geotools'
    }

    implementation 'com.opencsv:opencsv:5.7.1'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.16'
    testCompileOnly 'junit:junit:4.13.1'
}


mainClassName = 'org.noisemodelling.runner.Main'

jar {
    manifest {
        attributes('Class-Path': configurations.default.collect { it.getName() }.join(' '),
                'Main-Class': 'org.noisemodelling.runner.Main')
    }
}
